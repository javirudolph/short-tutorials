
    model {

    # Priors
    psi ~ dunif(0,1)
    r ~ dunif(0,1)
    p2 ~ dunif(0,1)

    # Multinomial logit link for observation model for state 3 (many bats)
    lp32 ~ dnorm(0, 0.001)
    lp33 ~ dnorm(0, 0.001)
    p32 <- exp(lp32) / (1 + exp(lp32) + exp(lp33))
    p33 <- exp(lp33) / (1 + exp(lp32) + exp(lp33))
    p31 <- 1-p32-p33

    # Define initial state vector Omega
    Omega[1] <- 1 - psi
    Omega[2] <- psi * (1-r)
    Omega[3] <- psi * r

    # Define observation matrix
    Theta[1,1] <- 1
    Theta[1,2] <- 0
    Theta[1,3] <- 0
    Theta[2,1] <- 1-p2
    Theta[2,2] <- p2
    Theta[2,3] <- 0
    Theta[3,1] <- p31
    Theta[3,2] <- p32
    Theta[3,3] <- p33

    # State-space likelihood
    # State equation: the true states
    for (i in 1:nsites) {
    z[i] ~ dcat(Omega[])
    }

    # Observation equation
    for (i in 1:nsites) {
      for (j in 1:nsurveys) {
      y[i,j] ~ dcat(Theta[z[i],])
      }
    }

    # Derived quantities
    for (i in 1:nsites) {
      occ1[i] <- equals(z[i], 1)
      occ2[i] <- equals(z[i], 2)
      occ3[i] <- equals(z[i], 3)
    }
    n.occ[1] <- sum(occ1[]) # Total number of sites in state 1
    n.occ[2] <- sum(occ2[])
    n.occ[3] <- sum(occ3[])

    }
    